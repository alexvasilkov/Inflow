apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.6'
}

task jacocoCoverage(type: JacocoReport, dependsOn: 'test') {
    group = 'Verification'
    description = 'Generate Jacoco coverage reports'

    check.dependsOn name

    def classesFilter = ['**/*$inlined$*.*']
    def classes = fileTree(dir: "$buildDir/classes/kotlin/main", excludes: classesFilter)
    def sources = files(new File(projectDir, 'src/main/kotlin'))
    def coverageData = fileTree(dir: new File(buildDir, 'jacoco'), include: '*.exec')

    classDirectories.setFrom classes
    additionalSourceDirs.setFrom sources
    sourceDirectories.setFrom sources
    executionData.setFrom coverageData

    reports {
        xml.enabled = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
        html.enabled = true
        html.destination = file("$buildDir/reports/jacoco/html/")
    }
}